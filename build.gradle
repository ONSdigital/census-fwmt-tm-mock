buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.2.RELEASE'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'application'
    id 'org.springframework.boot' version '2.1.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
}

mainClassName = 'uk.gov.ons.fwmt.census.tm.mock.Application'

description = 'fwmt-census-tm-mock'
group = 'uk.gov.ons.fwmt'
version = '3.0.0-SNAPSHOT'
sourceCompatibility = 11
targetCompatibility = 11

jar {
    group = 'uk.gov.ons.fwmt'
    baseName = 'fwmt-census-tm-mock'
    version = version
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-snapshot' }
    maven { url 'http://artifactory-sdc.onsdigital.uk/artifactory/libs-release' }
    maven { url 'https://oss.sonatype.org/content/repositories/libs-snapshot' }
    maven { url 'http://repo.maven.apache.org/maven2' }
}

configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "${projectDir}/src/main/resources/wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir) {
                arg(value: '-wsdl')
                produces(dir: sourcesDir, includes: '**/*.java')
                schema(dir: schema, includes: '**/*.wsdl')
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: 'lines,vars,source',
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: '**/*.java')
                include(name: '*.java')
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: '**/*.java')
                }
            }
        }
    }
}

dependencies {
    jaxb group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.1'
    jaxb group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.3.1'
    jaxb group: 'javax.activation', name: 'activation', version: '1.1.1'

    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0'
    compile group: 'com.sun.xml.bind', name: 'jaxb-core', version: '2.3.0.1'
    compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.3.1'
    compile group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.3.1'

    compile(files(genJaxb.classesDir).builtBy(genJaxb))

    compile group: 'uk.gov.ons.fwmt', name: 'fwmt-gateway-common', version: '0.0.8-RELEASE'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: springBootVersion
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-cloudfoundry-connector', version: '2.0.4.RELEASE') {
        exclude(module: 'log4j')
    }
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-spring-service-connector', version: '2.0.4.RELEASE') {
        exclude(module: 'log4j')
    }

    // this dependency is required to fix spring-boot-starter-web-services (and saaj-impl), which depend on an old non-existent version
    compile group: 'org.jvnet.mimepull', name: 'mimepull', version: '1.9.10'

    compile group: 'com.rabbitmq', name: 'amqp-client', version: '5.5.1'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.18.4'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.4'

    compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'

    compile group: 'org.json', name: 'json', version: '20180813'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.23'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.7'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.7'
    compile group: 'com.github.joschi.jackson', name: 'jackson-datatype-threetenbp', version: '2.6.4'
    compile group: 'javax.validation', name: 'validation-api'

    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion) {
        exclude(module: 'android-json')
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
